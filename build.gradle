buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
		
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "1.7.10-2.11"
group= "covers1624.powerconverters"
archivesBaseName = "PowerConverters"

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "eclipse"
}

String buildNo = "unknown"
if(System.getenv("BUILD_NUMBER") != null){
	buildNo = System.getenv("BUILD_NUMBER")
	version = version + "-#${buildNo}"
}


repositories {
	// Waila
	maven {
		name = "Mobius Repo"
		url "http://mobiusstrip.eu/maven/"
	}
	// All the CB stuff, Core, NEI, FMP and lib are on the forge maven.
	maven {
		name = "CB Repo"
		url "http://chickenbones.net/maven/"
	}
	// IC2
	maven {
		name = "ic2"
		url = "http://maven.ic2.player.to/"
	}
	// Buildcraft
    ivy {
        name "BuildCraft"
        artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision]-[classifier].[ext]"
    }
	// PneumaticCraft
	maven {
        name = "MM repo"
        url = "http://maven.k-4u.nl/"
    }
	//Covers1624
	maven {
		name ="Covers1624 Repo"
		url = "http://adel.podgeweb.com:1616/maven/"
	}
}

dependencies {
	compile "codechicken:CodeChickenLib:1.7.10-1.1.3.140:dev"
	compile "codechicken:CodeChickenCore:1.7.10-1.0.7.47:dev"
	compile "codechicken:NotEnoughItems:1.7.10-1.0.5.118:dev"
	compile "mcp.mobius.waila:Waila:1.5.11-RC1_1.7.10"// Waila
    compile name: "buildcraft", version: "7.1.8", classifier: "dev"// Buildcraft
	compile 'net.industrial-craft:industrialcraft-2:2.2.504-experimental:dev' //IC2
	compile "pneumaticCraft:PneumaticCraft-1.7.10:1.9.10-98:userdev"
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'covers1624.powerconverters.asm.ASMHandler'
		attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

task deobfJar(type: Jar, dependsOn: 'jar') {
	from sourceSets.main.output
	classifier "dev"
}

build.dependsOn deobfJar

artifacts {
	archives deobfJar
}

processResources{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
